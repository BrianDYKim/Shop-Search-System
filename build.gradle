buildscript {
    ext {
        springBootVersion = '2.7.5'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

repositories {
    mavenCentral()
}

allprojects {
    group = 'team.bakkas'
    version = '0.0.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        // lombok
        annotationProcessor("org.projectlombok:lombok")
        compileOnly("org.projectlombok:lombok")

        // Spring MVC
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        // for test
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.12.4'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.12.4'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

["domains"].each {
    def subProjectDir = new File(projectDir, it)

    subProjectDir.eachDir {dir ->
        def projectName = ":${it}-${dir.name}"

        // domain은 bootJar = false로 맞춰서 jar 생성을 막는다
        project(projectName) {
            bootJar.enabled(false)
            jar.enabled(true)
        }
    }
}

["event", "lambda"].each {
    def subProjectDir = new File(projectDir, it)

    subProjectDir.eachDir { dir ->
        def projectName = ":${it}-${dir.name}"
    }
}